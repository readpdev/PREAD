      *%METADATA                                                       *
      * %TEXT DocManager Install Program screens                       *
      *%EMETADATA                                                      *
     A*%%TS  SD  20160728  165214  PREAD       REL-V7R2M0  5770-WDS
     A* CRTOPT DFRWRT(*NO)
     A* CRTOPT SHARE(*YES)
     A************----------------
     A* SPYINSTFM  Install SpyView
     A************----------------
     A*
     A* PROGRAMMER: DAVID MICKLE            CREATION DATE   07/15/93
T5276A* 05-29-14 PLR Remove SpyWeb from selection option screen.
     A* 05-18-06 PLR Suppress install option for SpyWeb. No longer supported.
/8252A* 02-07-05 GT  Move copyright and company literals to PSCON
/8252A* 03-18-04 GT  Change company name and copyright year to 2005
/3929A* 07-30-04 GT  Change SpyWeb prompt message ID to LIC000A
/    A*               was displaying as ContentExplorer (LIC0009)
/INS8A* 03-18-04 GT  Change company name and copyright year to 2004
/INS8A* 05-01-03 GT  Change copyright year to 2003
/INS8A* 09-05-01 RA  REMOVE SPYWEB INSTALL - LEAVE IN CODE AS PER GT
/INS7A* 06-14-01 RA  IF MAJ. RELEASE, DISALLOW INSTALL NEW OBJECTS ONLY
/INS7A* 05-31-01 RA  LIMIT SPYWEB TO V4R2 AND ABOVE
/3765A* 03-26-01 RA  CHANGE NOTE CONVERSION FOR 7.1
/INS7A* 03-05-01 RA  ADD SUPPORT FOR WEB PTF
/INS7A* 01-09-01 RA  ADD NOTES CONVERSION PROMPTS
/3274A* 11-08-00 RA  Change Company Name to Gauss Interprise, Inc.
/2912A*  9-26-00 KAC ADD OPTION TO MOVE COMMANDS TO QSYS
     A* 11-04-98 JJF Add formats: BACKGND, CHOOSE, WEBINFO
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(93)
     A                                      CA12(12)
     A                                      CF10(50)
     A************----------------
     A          R CHOOSE
     A*%%TS  SD  20140529  093204  PREAD       REL-V7R1M0  5770-WDS
     A                                      WINDOW(7 2 14 42)
     A                                      KEEP
     A                                  1 12'Installation Menu'
     A                                      DSPATR(HI)
     A                                  3  2'Type options, press F10 to continu-
     A                                      e.'
     A                                      COLOR(BLU)
     A                                  4  4'1=Install product'
     A                                      COLOR(BLU)
     A                                  8  2'Opt'
     A                                      DSPATR(HI)
     A                                  8  7'Product'
     A                                      DSPATR(HI)
     A            OPT01          1A  B  9  3VALUES('1')
     A                                      CHECK(AB)
     A            OPT01DS       25A  O  9  7MSGID(LIC 0001 PSCON)
     A            OPT02          1A  B 10  3VALUES('1')
     A                                      CHECK(AB)
     A                                      DSPATR(ND)
     A                                      DSPATR(UL)
     A                                      DSPATR(PR)
     A            OPT02DS       25   O 10  7MSGID(LIC 0017 PSCON)
     A                                      DSPATR(ND)
     A            OPT03          1A  B 11  3VALUES('1')
     A                                      CHECK(AB)
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A            OPT03DS       25A  O 11  7MSGID(LIC 0012 PSCON)
     A                                      DSPATR(ND)
     A                                 13  3'F3=Exit'
     A                                      COLOR(BLU)
     A                                 13 13'F10=Continue'
     A                                      COLOR(BLU)
     A                                  6  2'Default language'
     A            LANGUAGE       2A  B  6 19VALUES('EN' 'FR' 'DE' 'PL')
     A                                  6 24'EN, FR, DE, PL'
     A************----------------
     A          R BACKGND
     A            M_CONAME      40A     1 21MSGID(MAG 000A PSCON)
     A                                      DSPATR(HI)
     A                                  1 62'Release:'
     A            RELESE         7A  O  1 73
     A  80                              2 73'BETA'
     A  80        NEWBTA         2A  O  2 78
     A                                  3  1'                                  -
     A                                                                         -
     A                                                 '
     A                                      DSPATR(UL)
     A                                  6 50'==============================='
     A                                      DSPATR(CS)
     A                                  7 51'-------'
     A                                      DSPATR(CS)
     A                                  7 59'W E L C O M E'
     A                                      DSPATR(HI)
     A                                  7 73'-------'
     A                                      DSPATR(CS)
     A                                  8 52'---------------------------'
     A                                      DSPATR(CS)
     A                                  9 53'----------'
     A                                      DSPATR(CS)
     A                                  9 64'T O'
     A                                      DSPATR(HI)
     A                                  9 68'----------'
     A                                      DSPATR(CS)
     A                                 10 54'-----------------------'
     A                                      DSPATR(CS)
     A                                 11 55'---------------------'
     A                                      DSPATR(CS)
     A                                 12 57'V I P  DocManager'
     A                                      DSPATR(UL)
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A                                 13 57'-----------------'
     A                                      DSPATR(CS)
     A                                 14 58'---------------'
     A                                      DSPATR(CS)
     A                                 15 59'-------------'
     A                                      DSPATR(CS)
     A                                 16 60'-----------'
     A                                      DSPATR(CS)
     A                                 17 61'---------'
     A                                      DSPATR(CS)
     A                                 18 62'-------'
     A                                      DSPATR(CS)
     A                                 19 63'-----'
     A                                      DSPATR(CS)
     A                                 20 64'---'
     A                                      DSPATR(CS)
     A                                 21 65'-'
     A                                      DSPATR(CS)
     A            M_COPYRT      52   O 24  2MSGID(P00 1154 PSCON)
     A                                      DSPATR(HI)
     A************----------------
     A          R BORDR
     A                                      FRCDTA
     A                                      TEXT('BORDER MENU')
     A                                      KEEP
     A                                  1 62'Release:'
     A            RELESE         7A  O  1 73
     A  80                              2 73'BETA'
     A  80        NEWBTA         2A  O  2 78
     A            M_CPYR1       23A  O  3 57MSGID(MAG 000C PSCON)
     A                                      DSPATR(HI)
     A            M_CPYR2       23A  O  4 57MSGID(MAG 000B PSCON)
     A                                      DSPATR(HI)
     A************----------------
     A          R WEBINFO
     A*%%TS  SD  20150216  131912  PREAD       REL-V6R1M0  5761-WDS
     A                                      WINDOW(2 1 17 49)
     A                                      CF06(96)
     A                                      KEEP
     A                                      BLINK
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRATR
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            OPT02DS       25A  O  1  1MSGID(LIC 0017 PSCON)
     A                                      DSPATR(HI)
     A                                  1 34'ACTION - INSTALL'
     A                                      DSPATR(HI)
     A                                  3  3'Install library ..........:'
     A                                      DSPATR(HI)
     A            SPYCGILIB     10A  B  3 31
     A                                  5  3'From Device...............:'
     A                                      DSPATR(HI)
     A            DEV           10A  B  5 31
     A                                  7  3'Owner of objects..........:'
     A                                      DSPATR(HI)
     A            SPYOBJOWN     10A  B  7 31
     A                                  9  3'New install ASP...........:'
     A                                      DSPATR(HI)
     A            ASP            2A  B  9 31
     A                                 11  3'Install PTFs only.........:'
     A                                      DSPATR(ND)
     A            INSWEBPTF      1A  B 11 31DSPATR(PR)
     A                                      DSPATR(ND)
     A                                 13  1'F3=Exit   F6=ReadMe   F10=Continue'
     A            DSPMS1        49A  O 14  1OVRDTA
     A                                      DSPATR(HI)
     A            DSPMS2        49A  O 15  1OVRDTA
     A                                      DSPATR(HI)
     A            DSPMS3        49A  O 16  1OVRDTA
     A                                      DSPATR(HI)
     A************----------------
     A          R VCOINFO
     A                                      CF06(96)
     A                                      KEEP
     A                                      BLINK
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRATR
     A                                      WINDOW(2 1 17 49)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            OPT03DS       25A  O  1  1MSGID(LIC 0012 PSCON)
     A                                      DSPATR(HI)
     A                                  1 34'ACTION - INSTALL'
     A                                      DSPATR(HI)

     A                                  3  3'Install library ..........:'
     A                                      DSPATR(HI)
     A            DOCVCOLIB     10A  B  3 31

     A                                  5  3'From Device...............:'
     A                                      DSPATR(HI)
     A            DEV           10A  B  5 31

     A                                  7  3'Owner of objects..........:'
     A                                      DSPATR(HI)
     A            SPYOBJOWN     10A  B  7 31

     A                                  9  3'New install ASP...........:'
     A                                      DSPATR(HI)
     A            ASP            2A  B  9 31

     A                                 11  3'Install PTFs only.........:'
     A                                      DSPATR(ND)
     A            INSVCOPTF      1A  B 11 31
     A                                      DSPATR(PR ND)

     A                                 13  1'F3=Exit   F6=ReadMe   F10=Continue'
     A            DSPMS1        49A  O 14  1OVRDTA
     A                                      DSPATR(HI)
     A            DSPMS2        49A  O 15  1OVRDTA
     A                                      DSPATR(HI)
     A            DSPMS3        49A  O 16  1OVRDTA
     A                                      DSPATR(HI)

     A************----------------
     A          R KEY
     A*%%TS  SD  20160728  165214  PREAD       REL-V7R2M0  5770-WDS
     A                                      KEEP
     A                                      BLINK
     A                                      LOCK
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRATR
     A                                      WINDOW(2 1 21 49)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A  90
     AO 91                                  RMVWDW
     A            OPT01DS       25A  O  1  1MSGID(LIC 0001 PSCON)
     A                                      DSPATR(HI)
     A                                  1 34'ACTION -'
     A                                      DSPATR(HI)
     A                                  1 43'INSTALL'
     A                                      DSPATR(HI)
     A                                  3  3'Current program library...:'
     A                                      DSPATR(HI)
     A            SPYOLDPGM     10A  B  3 31OVRDTA
     A  90
     AO 91                                  DSPATR(PR)
     A                                  4  3'Current data library......:'
     A                                      DSPATR(HI)
     A            SPYOLDDTA     10A  B  4 31OVRDTA
     A  90
     AO 91                                  DSPATR(PR)
     A                                  5  3'Current command library...:'
     A                                      DSPATR(HI)
     A            SPYOLDCMD     10A  B  5 31OVRDTA
     A  90
     AO 91                                  DSPATR(PR)
     A                                  7  3'Install program library...:'
     A                                      DSPATR(HI)
     A            SPYINSPGM     10A  B  7 31OVRDTA
     A  90
     AO 91                                  DSPATR(PR)
     A                                  8  3'Install data library......:'
     A                                      DSPATR(HI)
     A            SPYINSDTA     10A  B  8 31OVRDTA
     A  90
     AO 91                                  DSPATR(PR)
     A                                  9  3'Install command library...:'
     A                                      DSPATR(HI)
     A            SPYINSCMD     10A  B  9 31OVRDTA
     A  90
     AO 91                                  DSPATR(PR)
     A                                 11  3'From device...............:'
     A                                      DSPATR(HI)
     A            DEV           10A  B 11 31OVRDTA
     A  90
     AO 91                                  DSPATR(PR)
     A                                 13  3'Owner of objects..........:'
     A                                      DSPATR(HI)
     A            SPYOBJOWN     10A  B 13 31OVRDTA
     A  90
     AO 91                                  DSPATR(PR)
     A                                 14  3'New install ASP...........:'
     A                                      DSPATR(HI)
     A            ASP            2A  B 14 31OVRDTA
     A  90
     AO 91                                  DSPATR(PR)
     A                                 15  3'Install new objects only..:'
     A                                      DSPATR(HI)
     A            PTFONLY        1A  B 15 31OVRDTA
     A  94
     AO 90
     AO 91                                  DSPATR(PR)
     A                                 16  3'Install commands in QSYS..:'
     A                                      DSPATR(HI)
     A            CMDQSYS        1A  B 16 31OVRDTA
     A  90
     AO 91                                  DSPATR(PR)
     A                                 17  1'F3=Exit   F10=Continue'
     A            DSPMS1        49A  O 18  1OVRDTA
     A                                      DSPATR(HI)
     A            DSPMS2        49A  O 19  1OVRDTA
     A                                      DSPATR(HI)
     A            DSPMS3        49A  O 20  1OVRDTA
     A                                      DSPATR(HI)
     A                                 12  3'New install ASPDEV (IASP).:'
     A                                      DSPATR(HI)
     A            ASPDEV        10A  B 12 31
     A************----------------
     A          R BCKMSG
     A                                      WINDOW(7 3 10 45)
     A                                      BLINK
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  2  1'Warning:'
     A                                      DSPATR(BL)
     A                                  2 10'You are about to restore over your-
     A                                        '
     A                                  3 10'current release.  It is imperative-
     A                                        '
     A                                  4 10'that you make a backup of your    -
     A                                        '
     A                                  5 10'current DocManager libraries.  You-
     A                                      r '
     A                                  6 10'backup is a guarantee that your   -
     A                                        '
     A                                  7 10'current release will not be lost. -
     A                                        '
     A                                  9  1'F3=Exit'
     A                                  9 33'F10=Continue'
     A*****************************************************************
     A          R NOTCVSN
     A                                      WINDOW(2 2 21 75)
     A                                      BLINK
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  1 29'NOTES CONVERSION'
     A                                      DSPATR(HI)
     A                                  2  1'Warning:'
     A                                      DSPATR(BL)
     A                                  2 10'You are required to perform a conv-
     A                                      ersion of your notes'
     A                                  3 10'file for this release.  This conve-
     A                                      rsion procedure may'
     A                                  4 10'be a long running process dependin-
     A                                      g on your environment.'
     A                                  6 10'The install program will submit th-
     A                                      e notes conversion to a job'
     A                                  7 10'queue for processing after the ins-
     A                                      tallation is complete.'
     A                                  8 10'If you skip this submission, you m-
     A                                      ay execute command SPYNOTCVT'
     A                                  9 10'to perform the conversion at anoth-
     A                                      er time.'
     A                                 11 10'Do you wish to skip the submission-
     A                                      ?:'
     A            SKPNOTCVN      1A  B 11 47VALUES('Y' 'N')
     A                                 11 53'Y=Yes, N=No'
     A                                 13  5'Note Conversion options:'
     A                                      DSPATR(HI)
     A                                 14  7'Report Type. . . . . . . . .'
     A            NOTCVTFLD     10A  B 14 37
     A                                 14 53'Name, *ALL'
     A                                 16  7'Action for Old Note files. .'
     A            NOTCVTACT      7A  B 16 37VALUES('*DELETE' '*MOVE')
     A                                 16 53'*DELETE, *MOVE'
     A                                 17  7'Library for moved notes. . .'
     A            NOTCVTMVL     10A  B 17 37
     A                                 18  7'Job Queue Name . . . . . . .'
     A            NOTCVTJBQ     10A  B 18 37
     A  83                                  ERRMSG('Invalid Job Queue')
     A                                 19  9'Library  . . . . . . . . .'
     A            NOTCVTJBL     10A  B 19 39
     A                                 20  2'F3=Exit'  COLOR(BLU)
     A                                 20 62'F10=Continue' COLOR(BLU)
     A*****************************************************************
     A          R COPY2
     A*%%TS  SD  20150216  140820  PREAD       REL-V6R1M0  5761-WDS
     A                                      KEEP
     A                                      BLINK
     A                                      LOCK
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRATR
     A                                      WINDOW(4 7 17 49)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  1 30'ACTION -'
     A                                      DSPATR(HI)
     A                                  1 39'COPY/VERIFY'
     A                                      DSPATR(HI)
     A                                  1  2'All Objects & Files'
     A                                      DSPATR(HI)
     A            HIDE           1A  B  2  1DSPATR(ND)
     A                                      DSPATR(PR)
     A            L1            40A  O  3  3OVRDTA
     A            L2            40A  O  4  3OVRDTA
     A            L3            40A  O  5  3OVRDTA
     A            L4            40A  O  6  3OVRDTA
     A            L5            40A  O  7  3OVRDTA
     A            L6            40A  O  8  3OVRDTA
     A            L7            40A  O  9  3OVRDTA
     A            L8            40A  O 10  3OVRDTA
     A            L9            40A  O 11  3OVRDTA
     A            L10           40A  O 12  3OVRDTA
     A            C1             1A  O  3 45DSPATR(HI)
     A                                      OVRDTA
     A            V1             1A  O  3 48DSPATR(HI)
     A                                      OVRDTA
     A            C2             1A  O  4 45DSPATR(HI)
     A                                      OVRDTA
     A            V2             1A  O  4 48DSPATR(HI)
     A                                      OVRDTA
     A            C3             1A  O  5 45DSPATR(HI)
     A                                      OVRDTA
     A            V3             1A  O  5 48DSPATR(HI)
     A                                      OVRDTA
     A            C4             1A  O  6 45DSPATR(HI)
     A                                      OVRDTA
     A            V4             1A  O  6 48DSPATR(HI)
     A                                      OVRDTA
     A            C5             1A  O  7 45DSPATR(HI)
     A                                      OVRDTA
     A            V5             1A  O  7 48DSPATR(HI)
     A                                      OVRDTA
     A            C6             1A  O  8 45DSPATR(HI)
     A                                      OVRDTA
     A            V6             1A  O  8 48DSPATR(HI)
     A                                      OVRDTA
     A            C7             1A  O  9 45DSPATR(HI)
     A                                      OVRDTA
     A            V7             1A  O  9 48DSPATR(HI)
     A                                      OVRDTA
     A            C8             1A  O 10 45DSPATR(HI)
     A                                      OVRDTA
     A            V8             1A  O 10 48DSPATR(HI)
     A                                      OVRDTA
     A            C9             1A  O 11 45DSPATR(HI)
     A                                      OVRDTA
     A            V9             1A  O 11 48DSPATR(HI)
     A                                      OVRDTA
     A            C10            1A  O 12 45DSPATR(HI)
     A                                      OVRDTA
     A            V10            1A  O 12 48DSPATR(HI)
     A                                      OVRDTA
     A            DSPMS1        49A  O 14  1OVRDTA
     A                                      DSPATR(HI)
     A            DSPMS2        49A  O 15  1OVRDTA
     A                                      DSPATR(HI)
     A            DSPMS3        49A  O 16  1OVRDTA
     A                                      DSPATR(HI)
     A************----------------
     A          R DUMMY
     A                                      ASSUME
     A                                  1  3' '
     A          R QPMLPMGT
     A*%%TS  SD  20150217  085426  PREAD       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      WINDOW(*DFT 17 49)
     A                                      USRRSTDSP
     A                                  1  5'Warning! Object authority issue de-
     A                                      tected.'
     A                                      DSPATR(HI)
     A                                  3  1'DocManger requires object QPMLPMGT-
     A                                       *SRVPGM in '
     A                                  4  1'library QSYS to have *PUBLIC *USE -
     A                                      authority or   '
     A                                  5  1'equivalent. If authority is not pr-
     A                                      operly set, '
     A                                  6  1'this will impact the ability for s-
     A                                      ome users to'
     A                                  7  1'gain access to the product.'
     A                                  9  1'Options: 1. Set authority *PUBLIC -
     A                                      *USE.'
     A                                 10 10'2. Configure custom authority.'
     A                                 11 10'3. Ignore and continue install.'
     A                                 13 10'Choice:'
     A            OBJAUTCHC      1A  B 13 18DSPATR(HI)
     A                                      CHECK(ME)
     A                                      VALUES('1' '2' '3')
     A                                      DFTVAL('1')
