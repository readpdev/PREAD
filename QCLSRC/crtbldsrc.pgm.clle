      /*%METADATA                                                     */
      /* %TEXT Create Source Based Build Environment                  */
      /*%EMETADATA                                                    */
/*                                                                            */
/* 04-14-16 PLR Modify to handle ptf releases.                                */
/*                                                                            */
/* 02-25-12 PLR Create source build environment. When run to completion,      */
/*              all source and supporting data will be available for          */
/*              a build of the DMS system. J4017.                             */
/*                                                                            */
             PGM        (&SRCLIB &PGMLIB &DTALIB &TGTLIB &TGTRLS +
                          &IFSRLSDIR )

             DCLF       FILE(SAMPLEDATA)

             DCL        VAR(&SRCLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PGMLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTALIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TGTLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TGTRLS) TYPE(*CHAR) LEN(6)
             DCL        VAR(&IFSRLSDIR) TYPE(*CHAR) LEN(15)

             DCL        VAR(&SRCFNAMES) TYPE(*CHAR) LEN(150) VALUE('H      -
   QCLLESRC  QCLSRC    QCMDSRC   QCPPSRC   QCSRC -
    QDDSSRC   QLFSRC    QMNUSRC   QPFSRC    +
                          QPNLSRC   QPRTSRC   QRPGLESRC QRPGSRC   QSRVSRC  -
 ')
             DCL        VAR(&SRCFNAMEP) TYPE(*PTR) ADDRESS(&SRCFNAMES)
             DCL        VAR(&SRCFNAME) TYPE(*CHAR) STG(*BASED) LEN(10) +
                          BASPTR(&SRCFNAMEP)
             DCL        VAR(&TOFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&IFSSAVF) TYPE(*CHAR) LEN(128)
             DCL        VAR(&I) TYPE(*INT)
             DCL        VAR(&J) TYPE(*INT)
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(100)
             DCL        VAR(&IFSOMIT) TYPE(*CHAR) LEN(20)
             DCL        VAR(&RPGNAMS) TYPE(*CHAR) +
                          VALUE('MAG1030MAG1505MAG1507MAG8012')
             DCL        VAR(&RPGNAMP) TYPE(*PTR) ADDRESS(&RPGNAMS)
             DCL        VAR(&RPGNAM) TYPE(*CHAR) STG(*BASED) LEN(7) +
                          BASPTR(&RPGNAMP)
             DCL        VAR(&TOMBR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RCDLEN) TYPE(*INT)
             DCL        VAR(&WRKLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PTFVER) TYPE(*INT) VALUE(0)
             DCL        VAR(&PTFINT) TYPE(*INT)
             DCL        VAR(&PTFCHR) TYPE(*CHAR) LEN(1)

             /* Create target library. */
             DLTLIB     LIB(&TGTLIB)
             MONMSG     MSGID(CPF0000)
             CHKOBJ     OBJ(&TGTLIB) OBJTYPE(*LIB)
             MONMSG     MSGID(CPF9801) EXEC(CRTLIB LIB(&TGTLIB))

             /* Source and program library checking */
             CHGVAR     VAR(&PTFVER) VALUE(%SST(&PGMLIB 6 1))
             DOFOR      VAR(&PTFINT) FROM(0) TO(&PTFVER)
               CHGVAR     VAR(&PTFCHR) VALUE(&PTFINT)

               /* Source library checking */
               CHGVAR     VAR(&WRKLIB) VALUE(%SST(&SRCLIB 1 5) |< +
                            &PTFCHR |< %SST(&SRCLIB 7 3))
               CHKOBJ     OBJ(&WRKLIB) OBJTYPE(*LIB)
               MONMSG     MSGID(CPF9801) EXEC(DO)
                  CHGVAR     &MSG VALUE('Library' |> &WRKLIB |> 'does not +
                               exist.')
                  CALLSUBR   SUBR(SPM)
                  RETURN
               ENDDO

               /* Program library checking */
               CHGVAR     VAR(&WRKLIB) VALUE(%SST(&PGMLIB 1 5) |< +
                            &PTFCHR |< %SST(&PGMLIB 7 3))
               CHKOBJ     OBJ(&WRKLIB) OBJTYPE(*LIB)
               MONMSG     MSGID(CPF9801) EXEC(DO)
                  CHGVAR     &MSG VALUE('Library' |> &WRKLIB |> 'does not +
                               exist.')
                  CALLSUBR   SUBR(SPM)
                  RETURN
               ENDDO
             ENDDO

             /* Data Library Checking */
             CHKOBJ     OBJ(&DTALIB) OBJTYPE(*LIB)
             MONMSG     MSGID(CPF9801) EXEC(DO)
                CHGVAR     &MSG VALUE('Library' |> &DTALIB |> 'does not +
                             exist.')
                CALLSUBR   SUBR(SPM)
                RETURN
             ENDDO

             /* Build sample data files. */
 READLOOP:   RCVF
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ENDLOOP))
             CHGVAR     VAR(&TOFILE) VALUE('X' |< &MBFILE)
             CPYF       FROMFILE(&DTALIB/&MBFILE) TOFILE(&TGTLIB/&TOFILE) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF0000)
             GOTO       CMDLBL(READLOOP)

 ENDLOOP:    /* Copy the list of files containing sample data so it can */
             /* be used by the build script.                            */
             CPYF       FROMFILE(SAMPLEDATA) TOFILE(&TGTLIB/SAMPLEDATA) +
                          MBROPT(*REPLACE) CRTFILE(*YES)

             /* Copy source members to target library. */
             DOFOR      VAR(&I) FROM(1) TO(15)
                CHGVAR     VAR(&MSG) VALUE('Creating source file' |> +
                             &TGTLIB |< '/' |< &SRCFNAME)
                CALLSUBR   SPM
                CHGVAR     VAR(&RCDLEN) VALUE(200)
                IF         COND(&SRCFNAME *EQ 'QDDSSRC') THEN(CHGVAR +
                             VAR(&RCDLEN) VALUE(132))
                CRTSRCPF   FILE(&TGTLIB/&SRCFNAME) RCDLEN(&RCDLEN)
                MONMSG     MSGID(CPF0000)

                /* Loop through base then ptf libraries */
                DOFOR      VAR(&PTFINT) FROM(0) TO(&PTFVER)

                   /* Source library checking */
                   CHGVAR     VAR(&PTFCHR) VALUE(&PTFINT)
                   CHGVAR     VAR(&WRKLIB) VALUE(%SST(&SRCLIB 1 5) |< +
                                &PTFCHR |< %SST(&SRCLIB 7 3))

                   CPYSRCF    FROMFILE(&WRKLIB/&SRCFNAME) +
                                TOFILE(&TGTLIB/&SRCFNAME) FROMMBR(*ALL) +
                                TOMBR(*FROMMBR) TOMBRID(*FROMMBR) +
                                MBROPT(*REPLACE)
                   MONMSG     MSGID(CPF0000)

                ENDDO      /* ptfint = 0 to ptfver */

                CHGVAR     VAR(%OFFSET(&SRCFNAMEP)) +
                             VALUE(%OFFSET(&SRCFNAMEP) + 10)

             ENDDO      /* i = 1 to 15 */

             /* Convert RPG source to ILE */
             CVTRPGSRC  FROMFILE(&TGTLIB/QRPGSRC) FROMMBR(*ALL) +
                          TOFILE(&TGTLIB/QRPGLESRC) CVTRPT(*NO) +
                          LOGFILE(*NONE)
             DLTF       FILE(&TGTLIB/QRPGSRC)

             /* Convert handful of RPG source members in QRPGLESRC */
             DOFOR      VAR(&J) FROM(1) TO(4)
                CHGVAR     &TOMBR VALUE('X' |< &RPGNAM)
                CVTRPGSRC  FROMFILE(&TGTLIB/QRPGLESRC) FROMMBR(&RPGNAM) +
                             TOFILE(&TGTLIB/QRPGLESRC) TOMBR(&TOMBR) +
                             CVTRPT(*NO) LOGFILE(*NONE)
                MONMSG     MSGID(CPF0000)
                RMVM       FILE(&TGTLIB/QRPGLESRC) MBR(&RPGNAM)
                MONMSG     CPF0000
                RNMM       FILE(&TGTLIB/QRPGLESRC) MBR(&TOMBR) +
                             NEWMBR(&RPGNAM)
                MONMSG     CPF0000
                CHGVAR     VAR(%OFFSET(&RPGNAMP)) VALUE(%OFFSET(&RPGNAMP) +
                             + 7)
             ENDDO

             /* Copy in the source that sets the libraries in the tables */
             /* from the install source...DINXXXSRC/QRPGLESRC,CHGSPYLIB */
             CHGVAR     VAR(&WRKLIB) VALUE('DIN' |< %SST(&SRCLIB 4 +
                          2) |< '0SRC')
             CPYSRCF    FROMFILE(&WRKLIB/QRPGLESRC) +
                          TOFILE(&TGTLIB/QRPGLESRC) +
                          FROMMBR(CHGSPYLIB) TOMBR(*FROMMBR) +
                          TOMBRID(*FROMMBR) MBROPT(*REPLACE)

             /* Save IFS supporting data. */
             CHGVAR     &MSG VALUE('Saving IFS supporting data')
             CALLSUBR   SPM
             CRTSAVF    FILE(&TGTLIB/IFSDATA)
             MONMSG     MSGID(CPF0000)
             CHGVAR     VAR(&IFSSAVF) VALUE('/QSYS.LIB/' |< &TGTLIB |< +
                          '.LIB/IFSDATA.FILE')
             CHGVAR     &IFSOMIT VALUE(&IFSRLSDIR |< '/Update/*')
             SAV        DEV(&IFSSAVF) OBJ((&IFSRLSDIR *INCLUDE) +
                          (&IFSOMIT *OMIT)) CLEAR(*REPLACE)

             /* Copy all message files to target library. */
             CHGVAR     VAR(&MSG) VALUE('Saving language message files')
             CALLSUBR   SPM

             DOFOR      VAR(&PTFINT) FROM(0) TO(&PTFVER)
                CHGVAR     VAR(&PTFCHR) VALUE(&PTFINT)
                CHGVAR     VAR(&WRKLIB) VALUE(%SST(&PGMLIB 1 5) |< &PTFCHR +
                             |< %SST(&PGMLIB 7 3))
                DLTF       FILE(QTEMP/TEMPCOPY)
                MONMSG     MSGID(CPF0000)
                CRTSAVF    FILE(QTEMP/TEMPCOPY)
                SAVOBJ     OBJ(PSCON*) LIB(&WRKLIB) DEV(*SAVF) +
                             OBJTYPE(*MSGF) SAVF(QTEMP/TEMPCOPY)
                RSTOBJ     OBJ(PSCON*) SAVLIB(&WRKLIB) DEV(*SAVF) +
                             OBJTYPE(*MSGF) SAVF(QTEMP/TEMPCOPY) +
                             MBROPT(*ALL) ALWOBJDIF(*ALL) RSTLIB(&TGTLIB)
                MONMSG     MSGID(CPF0000)
               /* Delete the PSCON message file to save space. One of the */
               /* PSCONXX will be copied to PSCON during the build process. */
             ENDDO
             DLTMSGF    MSGF(&TGTLIB/PSCON)
             MONMSG     MSGID(CPF0000)

             /* Save template source file/members from data library. */
             CHGVAR     VAR(&MSG) VALUE('Copying sample and template +
                          source files.')
             CALLSUBR   SUBR(SPM)
             CRTSRCPF   FILE(&TGTLIB/FAXSRC) RCDLEN(192)
             MONMSG     MSGID(CPF0000)
             CPYSRCF    FROMFILE(&DTALIB/FAXSRC) +
                          TOFILE(&TGTLIB/FAXSRC) FROMMBR(*ALL) +
                          TOMBR(*FROMMBR) TOMBRID(*FROMMBR) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF0000)
             CRTSRCPF   FILE(&TGTLIB/QNDXSRC) RCDLEN(92)
             MONMSG     MSGID(CPF0000)
             CPYSRCF    FROMFILE(&DTALIB/QNDXSRC) +
                          TOFILE(&TGTLIB/QNDXSRC) FROMMBR(*ALL) +
                          TOMBR(*FROMMBR) TOMBRID(*FROMMBR) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF0000)
             CRTSRCPF   FILE(&TGTLIB/XQRPGLESRC) RCDLEN(192)
             MONMSG     MSGID(CPF0000)
             CPYSRCF    FROMFILE(&DTALIB/QRPGLESRC) +
                          TOFILE(&TGTLIB/XQRPGLESRC) FROMMBR(*ALL) +
                          TOMBR(*FROMMBR) TOMBRID(*FROMMBR) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF0000)
             CRTSRCPF   FILE(&TGTLIB/XQCLSRC) RCDLEN(192)
             MONMSG     MSGID(CPF0000)
             CPYSRCF    FROMFILE(&DTALIB/QCLSRC) +
                          TOFILE(&TGTLIB/XQCLSRC) FROMMBR(*ALL) +
                          TOMBR(*FROMMBR) MBROPT(*REPLACE)
             MONMSG     MSGID(CPF0000)

             /* Save the target library to save file DOCBLDSRC in the */
             /* target library. Omit from save list. */
             CHGVAR     VAR(&MSG) VALUE('Saving build library to save +
                          file' |> &TGTLIB |< '/' |< 'DOCBLDSRC')
             CALLSUBR   SPM
             CRTSAVF    FILE(&TGTLIB/DOCBLDSRC)
             MONMSG     MSGID(CPF0000)
             SAVLIB     LIB(&TGTLIB) DEV(*SAVF) +
                          SAVF(&TGTLIB/DOCBLDSRC) TGTRLS(&TGTRLS) +
                          UPDHST(*NO) CLEAR(*REPLACE) DTACPR(*HIGH) +
                          OMITOBJ((&TGTLIB/DOCBLDSRC *FILE))

             /* Send completion message. */
             CHGVAR     VAR(&MSG) VALUE('Build data completed and saved.' +
                          |> &TGTLIB)
             CALLSUBR   SUBR(SPM)


             /* Send program message */
             SUBR       SUBR(SPM)
                SNDPGMMSG  MSGID(CPF9897) MSGF(QCPFMSG) MSGDTA(&MSG) +
                             TOPGMQ(*EXT) MSGTYPE(*COMP)
             ENDSUBR

             ENDPGM

