      *%METADATA                                                       *
      * %TEXT Variables Definitions                                    *
      *%EMETADATA                                                      *
      * CRTOPT MBR(*NONE)
      ****************************************************************
      *                    Magellan Software                         *
      *                                                              *
      * Physical file..... VARDEFS - Field reference file            *
      * Date created...... 3/10/93                                   *
      *                                                              *
      *--------------------------------------------------------------*
      * DM  1/28/94 ADDED LOCSFC AND PKVER                           *
      *                                                              *
      ****************************************************************
     A          R FLDREF
      ************************************
      **** System Header
      ************************************
1    A            VERSIO         3A         TEXT('Version')
     A                                      COLHDG('Version')
2    A            PTFLVL         2A         TEXT('PTF Level')
     A                                      COLHDG('PTF Lvl')
3    A            BTALVL         2A         TEXT('Beta Level')
     A                                      COLHDG('Beta Lvl')
4    A            CREDAT         9B 0       TEXT('Creation Date')
     A                                      COLHDG('Creation' 'Date')
5    A            CRETIM         9B 0       TEXT('Creation Time')
     A                                      COLHDG('Creation' 'Time')
6    A            LACDAT         9B 0       TEXT('Last Accessed Date')
     A                                      COLHDG('Last' 'Accessed' 'Date')
7    A            LACTIM         9B 0       TEXT('Last Accessed Time')
     A                                      COLHDG('Last' 'Accessed' 'Time')
8    A            NUMFLD         9B 0       TEXT('Number of Folders')
     A                                      COLHDG('Number' 'of' 'Folders')
9    A            ADESC         30A         TEXT('Archive Description')
     A                                      COLHDG('Archive' 'Description')
10   A            OQMNUM         6A         TEXT('OutQ Monitor Number')
     A                                      COLHDG('OutQ' 'Monitor' 'Number')
11   A            OQMUSR        10A         TEXT('OutQ Monitor User')
     A                                      COLHDG('OutQ' 'Monitor' 'User')
12   A            OQMJOB        10A         TEXT('OutQ Monitor Job')
     A                                      COLHDG('OutQ' 'Monitor' 'Job')
13   A            FILLR1         1
      ************************************
      **** Folder Names Directory
      ************************************
1  K A            FLDCOD        10A         TEXT('Folder Code')
     A                                      COLHDG('Folder' 'Code')
2  K A            FLDLIB        10A         TEXT('Folder Library')
     A                                      COLHDG('Folder' 'Library')
3    A            NUMFIL         9B 0       TEXT('Number Online Reports')
     A                                      COLHDG('Number' 'Online' 'Reports')
4    A            INTEOF         9B 0       TEXT('Current Internal EOF')
     A                                      COLHDG('Internal' 'EOF')
5    A            DLTRPT         9B 0       TEXT('Deleted Reports')
     A                                      COLHDG('Deleted' 'Reports')
6    A            FDESC         30A         TEXT('Folder Description')
     A                                      COLHDG('Archive' 'Description')
7    A            FLDLOC         1A         TEXT('Folder Location')
     A                                      COLHDG('Folder' 'Location')
8    A            OFFVOL        10A         TEXT('Offline Volume')
     A                                      COLHDG('Offline' 'Volume')
9    A            OFFDAT         9B 0       TEXT('Offline Date')
     A                                      COLHDG('Offline' 'Date')
10   A            NUMOFF         9B 0       TEXT('Number Offline Reports')
     A                                      COLHDG('Number' 'Offline' 'Reports')
11   A            FILLR2         2
      ************************************
      ****  Report Directory
      ************************************
1  K A            FLDR          10A         TEXT('Folder')
     A                                      COLHDG('Folder')
2  K A            FLDRLB        10A         TEXT('Folder Library')
     A                                      COLHDG('Folder' 'Library')
3  K A            FILNAM        10A         TEXT('File')
     A                                      COLHDG('File')
4  K A            JOBNAM        10A         TEXT('Job')
     A                                      COLHDG('Job')
5  K A            USRNAM        10A         TEXT('User Name')
     A                                      COLHDG('User' 'Name')
6  K A            JOBNUM         6A         TEXT('Job Number')
     A                                      COLHDG('Job' 'Number')
7  K A            FILNUM         9B 0       TEXT('File Number')
     A                                      COLHDG('File' 'Number')
8    A            CHKSUM         9B 0       TEXT('Check Sum')
     A                                      COLHDG('Check' 'Sum')
9    A            SFDESC        30A         TEXT('Spool File Description')
     A                                      COLHDG('Spool' 'File' 'Description')
10   A            FRMTYP        10A         TEXT('Form Type')
     A                                      COLHDG('Form' 'Type')
11   A            USRDTA        10A         TEXT('User Data')
     A                                      COLHDG('User' 'Data')
12   A            HLDF          10A         TEXT('Hold')
     A                                      COLHDG('Hold')
13   A            SAVF          10A         TEXT('Save')
     A                                      COLHDG('Save')
14   A            TOTPAG         9B 0       TEXT('Number of Pages')
     A                                      COLHDG('Number' 'of' 'Pages')
15   A            TOTCPY         9B 0       TEXT('Copies')
     A                                      COLHDG('Copies')
16   A            LPI            9B 0       TEXT('LPI')
     A                                      COLHDG('LPI')
17   A            CPI            9B 0       TEXT('CPI')
     A                                      COLHDG('CPI')
18   A            OUTQNM        10A         TEXT('Output Queue')
     A                                      COLHDG('Output' 'Queue')
19   A            OUTQLB        10A         TEXT('Library')
     A                                      COLHDG('Library')
20   A            DATFOP         9B 0       TEXT('Spool File Creation Date')
     A                                      COLHDG('SplFil' 'Creation' 'Date')
21   A            TIMFOP         9B 0       TEXT('Spool File Creation Time')
     A                                      COLHDG('SplFil' 'Creation' 'Time')
22   A            DEVFNA        10A         TEXT('Device')
     A                                      COLHDG('Device')
23   A            PGMOPF        10A         TEXT('Create Program')
     A                                      COLHDG('Create' 'Program')
24   A            PRTTXT        30A         TEXT('Print Text')
     A                                      COLHDG('Print' 'Text')
25   A            DEVCLS        10A         TEXT('Device Type')
     A                                      COLHDG('Device' 'Type')
26   A            OVRLIN         9B 0       TEXT('Overflow')
     A                                      COLHDG('Overflow')
27   A            PRTFON        10A         TEXT('Font')
     A                                      COLHDG('Font')
28   A            PAGROT         9B 0       TEXT('Page Rotation')
     A                                      COLHDG('Page' 'Rotation')
29   A            MAXSIZ         9B 0       TEXT('Max Record Length')
     A                                      COLHDG('Max' 'Record' 'Length')
30   A            ADSF           9B 0       TEXT('Spool File Archive Date')
     A                                      COLHDG('SplFil' 'Archive' 'Date')
31   A            EXPDAT         9B 0       TEXT('Spool File Expiration Date')
     A                                      COLHDG('SplFil' 'Expiration' 'Date')
32   A            LOCSFA         9B 0       TEXT('Spool File Attr Location')
     A                                      COLHDG('SplFil' 'Attr' 'Location')
33   A            LOCSFD         9B 0       TEXT('Spool File Data Location')
     A                                      COLHDG('SplFil' 'Data' 'Location')
34   A            LOCSFP         9B 0       TEXT('Spool File PagTbl Location')
     A                                      COLHDG('SplFil' 'PagTbl' 'Location')
35   A            LOCSFC         9B 0       TEXT('Spool File PakTbl Location')
     A                                      COLHDG('SplFil' 'PakTbl' 'Location')
36   A            PAGSIZ         9B 0       TEXT('Page Size')
     A                                      COLHDG('Page' 'Size')
37   A            RECOVR         1A         TEXT('Recover Overflow')
     A                                      COLHDG('Recover' 'Overflow')
38   A            REPIND        10A         TEXT('Report Index')
     A                                      COLHDG('Report' 'Index')
39   A            RPIXNM        10A         TEXT('Report Index Name')
     A                                      COLHDG('Report' 'Index' 'Name')
40   A            DFTPRT        10A         TEXT('Default Printer')
     A                                      COLHDG('Default' 'Printer')
41   A            DPDESC        30A         TEXT('Default Printer Description')
     A                                      COLHDG('Default' 'Printer' 'Desc')
42   A            REPLOC         1A         TEXT('Report Location')
     A                                      COLHDG('Report' 'Location')
43   A            OFRVOL        10A         TEXT('Offline Rep. Volume')
     A                                      COLHDG('Offline' 'Rep.' 'Volume')
44   A            OFRDAT         9B 0       TEXT('Offline Rep. Date')
     A                                      COLHDG('Offline' 'Rep.' 'Date')
45   A            OFRNAM        10A         TEXT('Offline Rep. Name')
     A                                      COLHDG('Offline' 'Rep.' 'Name')
46   A            PKVER          1A         TEXT('Compression Ver.')
     A                                      COLHDG('Compress' 'Ver.')
47   A            FILLR3         1
